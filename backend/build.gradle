plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'java'
    id 'org.asciidoctor.jvm.convert' version "3.3.2"
    //grpc plugin
    id 'com.google.protobuf' version '0.8.18'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    asciidoctorExtensions
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'

    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'com.h2database:h2'
    implementation 'mysql:mysql-connector-java'

    compileOnly 'org.projectlombok:lombok'
    testCompileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testAnnotationProcessor 'org.projectlombok:lombok'

    implementation 'org.springframework.boot:spring-boot-starter-security'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    implementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
//    asciidoctor 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    asciidoctorExtensions 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    /**
     * grpc start
     */
    runtimeOnly 'io.grpc:grpc-netty-shaded:1.50.0'
    implementation 'io.grpc:grpc-stub:1.50.0'
    implementation 'io.grpc:grpc-protobuf:1.50.0'
    implementation 'io.grpc:grpc-gprc-stub:1.50.0'
    compileOnly 'org.apache.tomcat:annotations-api:6.0.53'
    /**
     * grpc end
     */

}
ext {
    snippetsDir = file('build/generated-snippets')
}
asciidoctor {
    dependsOn test
    configurations 'asciidoctorExtensions'
    inputs.dir snippetsDir
}
test {
    outputs.dir snippetsDir
}
asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}
bootJar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}
task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from "${asciidoctor.outputDir}/html5"
    into "${sourceSets.main.output.resourcesDir}/static/docs"
}
build {
    dependsOn copyDocument
}

tasks.named('test') {
    useJUnitPlatform()
}
//grpc task
protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.21.7"
    }
    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.50.0'
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}
